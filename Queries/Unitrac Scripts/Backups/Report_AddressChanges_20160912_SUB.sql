USE [Unitrac_Reports]
GO

/****** Object:  StoredProcedure [dbo].[Report_AddressChanges]    Script Date: 9/12/2016 7:34:31 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


ALTER PROCEDURE [dbo].[Report_AddressChanges] 
(
@FromDt as datetime2(7)= NULL,
@ToDt as datetime2(7) = NULL
)
AS

BEGIN

IF @FromDt IS NULL
BEGIN
set @FromDt = (select TOP 1 d.LastRunTime
    FROM  [RPTSERVER].ReportServer.dbo.Subscriptions d 
            JOIN [RPTSERVER].ReportServer.dbo.Catalog e ON d.report_oid = e.itemid
    WHERE e.name = 'AddressChanges'
    ORDER BY LastRunTime ASC)

	IF @FromDt IS NULL
	 SET @FromDt = DateAdd(d, -1, GETDATE())
END

IF @ToDt IS NULL
	SET @ToDt = GETDATE()
 

SELECT 
CASE WHEN BIA.ID IS NOT NULL THEN 'A' + CAST(BIA.ID AS VARCHAR(30)) ELSE 'C' + CAST(BIC.ID AS VARCHAR(30)) END AS [VENDOR_ID],
CASE WHEN BIA.NAME_TX IS NOT NULL THEN BIA.NAME_TX ELSE BIC.NAME END AS [VENDOR_NAME_TX],
A.ID AS [RADDR_ID],
CASE WHEN ISNULL(cu.QUALIFIER_TX,'') <> '' then cu.QUALIFIER_TX
	WHEN ISNULL(cu.AREA_TX,'') <> '' then cu.AREA_TX
	ELSE cu.column_nm END AS [QUALIFIER_TX],
CU.FROM_VALUE_TX AS [FROM_VALUE_TX],
CASE WHEN CU.OPERATION_CD = 'U' THEN CU.TO_VALUE_TX 
ELSE CASE WHEN CU.TABLE_NAME_TX = 'BORROWER_INSURANCE_AGENCY' THEN 'Created: ' + BIA.NAME_TX 
	    WHEN CU.TABLE_NAME_TX = 'BORROWER_INSURANCE_COMPANY' THEN 'Created: ' + BIC.NAME
        WHEN CU.TABLE_NAME_TX = 'ADDRESS' THEN 'Created: ' + ISNULL(A.LINE_1_TX, '') + ' | ' + ISNULL(A.LINE_2_TX,'') + ' | ' + ISNULL(A.CITY_TX,'') + ', ' + ISNULL(A.STATE_PROV_TX,'') + ' ' + ISNULL(A.POSTAL_CODE_TX,'') 
	END
END AS [TO_VALUE_TX],
C.USER_TX as [USER_TX],
C.CREATE_DT AS [CREATE_DT],
C.NOTE_TX AS [NOTE_TX],
C.ID as [CHANGE_ID],
@FromDt as [FROM_DT],
@ToDt as [TO_DT],
NULL as [REPORT_SORTBY_TX]
FROM [ADDRESS] A 
LEFT JOIN BORROWER_INSURANCE_COMPANY_ADDRESS_RELATE BICAR ON A.ID = BICAR.ADDRESS_ID AND BICAR.PURGE_DT IS NULL
LEFT JOIN BORROWER_INSURANCE_COMPANY BIC on BIC.ID = BICAR.BORROWER_INSURANCE_COMPANY_ID AND BIC.PURGE_DT IS NULL
LEFT JOIN BORROWER_INSURANCE_AGENCY BIA ON BIA.ESCROW_REMITTANCE_ADDRESS_ID = A.ID AND BIA.PURGE_DT IS NULL
LEFT JOIN CHANGE_UPDATE CU ON ((CU.TABLE_NAME_TX = 'BORROWER_INSURANCE_AGENCY' AND CU.TABLE_ID = BIA.ID) OR (CU.TABLE_NAME_TX = 'BORROWER_INSURANCE_COMPANY' AND CU.TABLE_ID = BIC.ID) OR (CU.TABLE_NAME_TX = 'ADDRESS' AND CU.TABLE_ID = A.ID)) and CU.CREATE_DT between @FromDt and @ToDt
LEFT JOIN CHANGE C ON CU.CHANGE_ID = C.ID 
WHERE A.UPDATE_DT between @FromDt and @ToDt
AND A.CERTIFIED_IN = 'Y'
ORDER BY A.id,cu.CREATE_DT desc

END

GO

