USE [UniTrac]
GO

/****** Object:  StoredProcedure [dbo].[GetAgencyRemittanceAddressList]    Script Date: 8/12/2015 2:22:01 PM ******/
DROP PROCEDURE [dbo].[GetAgencyRemittanceAddressList]
GO

/****** Object:  StoredProcedure [dbo].[GetAgencyRemittanceAddressList]    Script Date: 8/12/2015 2:22:01 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE PROCEDURE [dbo].[GetAgencyRemittanceAddressList]
(
  @agencyId bigint,
  @lenderId bigint,
  @branchId bigint = null,
  @payeeType varchar(5)
)
AS
BEGIN
	
	SET NOCOUNT ON

   DECLARE @USESUBCOMP AS NVARCHAR(10)
	
	SELECT @USESUBCOMP = RD.VALUE_TX 
  FROM RELATED_DATA RD 
  JOIN RELATED_DATA_DEF RDD ON RDD.ID = RD.DEF_ID AND RDD.NAME_TX = 'UseBorrowerInsuranceSubCompanies'
	WHERE RD.RELATE_ID = @lenderID
	
	IF OBJECT_ID(N'tempdb..#LPCM',N'U') IS NOT NULL
       DROP TABLE #LPCM   
       
	Create Table #LPCM
	(
	  LENDER_PAYEE_CODE_FILE_ID bigint,
	  LPCM_ID bigint,
	  BIC_ID BIGINT,
	  REMITTANCE_ADDR_ID bigint,	  
	  REMITTANCE_TYPE_CD nvarchar(20),
	  PAYEE_CODE_TX nvarchar(40),
     PRIMARY_IN char(1)
	)
	
	INSERT INTO #LPCM
	(
	  LENDER_PAYEE_CODE_FILE_ID,
	  LPCM_ID,
	  BIC_ID,
	  REMITTANCE_ADDR_ID,
	  REMITTANCE_TYPE_CD,
	  PAYEE_CODE_TX,
     PRIMARY_IN 
	)
	SELECT DISTINCT 
	      LPCM.LENDER_PAYEE_CODE_FILE_ID,
	      LPCM.ID AS LPCM_ID ,
		  LPCM.BIC_ID, 
	      LPCM.REMITTANCE_ADDR_ID,
	      LPCM.REMITTANCE_TYPE_CD,
		   LPCF.PAYEE_CODE_TX,
         LPCM.PRIMARY_IN 		 		  
		 FROM LENDER_PAYEE_CODE_MATCH LPCM
		 JOIN LENDER_PAYEE_CODE_FILE LPCF ON LPCF.ID = LPCM.LENDER_PAYEE_CODE_FILE_ID
		 WHERE 
		   LPCF.AGENCY_ID = @agencyId AND
		   LPCF.LENDER_ID = @lenderId AND
		   ((ISNULL(@branchId,0) > 0 AND LPCF.BRCH_LENDER_ORG_ID = @branchId) OR
		    (ISNULL(@branchId,0) = 0 AND LPCF.BRCH_LENDER_ORG_ID IS NULL)
		   )	
         AND LPCM.PURGE_DT IS NULL
		   AND LPCF.PURGE_DT IS NULL
	
  
	if (@payeeType = 'BIC')
    BEGIN
		SELECT  
			  'BIC' AS PAY_TYPE,
			  BIC.ID,
			  REL.ADDRESS_ID,
			  BIC.NAME ,
			  ADDR.LINE_1_TX,
			  ADDR.LINE_2_TX,
			  ADDR.ATTENTION_TX,
			  ADDR.CITY_TX,
			  ADDR.STATE_PROV_TX,
			  ADDR.POSTAL_CODE_TX,
			  ADDR.CERTIFIED_IN,
			  CASE
			  	  WHEN ADDR.ADDITIONAL_DATA_XML IS NOT NULL
			  	  THEN ADDR.ADDITIONAL_DATA_XML.value('(/AdditionalData/CheckName/text())[1]', 'varchar(300)')
			  	ELSE ''
			  END AS 'CheckName',
			  LPM.LENDER_PAYEE_CODE_FILE_ID,
			  LPM.REMITTANCE_ADDR_ID,
			  LPM.LPCM_ID ,
			  LPM.PAYEE_CODE_TX,
			  LPM.REMITTANCE_TYPE_CD,
           LPM.PRIMARY_IN,
           SUBCOMP_IN = CASE WHEN BIC.PARENT_ID IS NOT NULL THEN 'Y'
			                    ELSE 'N' END
		FROM BORROWER_INSURANCE_COMPANY BIC
		  JOIN BORROWER_INSURANCE_COMPANY_ADDRESS_RELATE REL ON 
			REL.BORROWER_INSURANCE_COMPANY_ID = BIC.ID
		 JOIN ADDRESS ADDR ON ADDR.ID = REL.ADDRESS_ID
		 AND ADDR.ADDRESS_TYPE_CD = 'REMT' AND REL.ACTIVE_IN = 'Y'
		 OUTER APPLY
		 (
			SELECT LPCM.* FROM #LPCM LPCM
			   WHERE LPCM.REMITTANCE_ADDR_ID = ADDR.ID
			   AND LPCM.BIC_ID = BIC.ID
			   AND LPCM.REMITTANCE_TYPE_CD = 'BIC'
		 ) AS LPM
		 WHERE (ISNULL(@USESUBCOMP,'FALSE') = 'TRUE' OR 
		        (ISNULL(@USESUBCOMP,'FALSE') = 'FALSE' AND BIC.PARENT_ID IS NULL)
		       )
		 AND BIC.AGENCY_ID = @agencyId
		 AND BIC.PURGE_DT IS NULL
		 AND REL.PURGE_DT IS NULL
     AND ADDR.PURGE_DT IS NULL
       END
     ELSE IF (@payeeType = 'BIA')
       BEGIN
		 SELECT 
			  'BIA' AS PAY_TYPE,
			  BIA.ID,
			  BIA.ESCROW_REMITTANCE_ADDRESS_ID AS ADDRESS_ID,
			  BIA.NAME_TX AS NAME,
			  ADDR.LINE_1_TX,
			  ADDR.LINE_2_TX,
			  null as 'ATTENTION_TX',
			  ADDR.CITY_TX,
			  ADDR.STATE_PROV_TX,
			  ADDR.POSTAL_CODE_TX,
			  null as 'CERTIFIED_IN',
			  null as 'CheckName',
			  LPM.LENDER_PAYEE_CODE_FILE_ID,
			  LPM.REMITTANCE_ADDR_ID,
			  LPM.LPCM_ID ,
			  LPM.PAYEE_CODE_TX,
			  LPM.REMITTANCE_TYPE_CD,
           LPM.PRIMARY_IN,
           'N' AS SUBCOMP_IN
		 FROM
		 BORROWER_INSURANCE_AGENCY BIA
		 JOIN ADDRESS ADDR ON ADDR.ID = BIA.ESCROW_REMITTANCE_ADDRESS_ID
		 AND ADDR.ADDRESS_TYPE_CD = 'REMT' 
		 OUTER APPLY
		 (
			SELECT LPCM.* FROM #LPCM LPCM
			   WHERE LPCM.REMITTANCE_ADDR_ID = ADDR.ID
			   AND LPCM.REMITTANCE_TYPE_CD = 'BIA'
		 ) AS LPM
		 WHERE 
		 BIA.AGENCY_ID = @agencyId
		 AND BIA.ACTIVE_IN = 'Y'
		 AND BIA.PURGE_DT IS NULL
		 AND ADDR.PURGE_DT IS NULL
       END
   
END


GO

