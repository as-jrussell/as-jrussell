USE [UniTrac]
GO

/****** Object:  UserDefinedFunction [dbo].[GetQCMortgageInadequateCoverage]    Script Date: 8/25/2021 4:09:17 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE FUNCTION [dbo].[GetQCMortgageInadequateCoverage]
(
	@lenderIds NVARCHAR(MAX),
	@lastRunDate DATETIME
)

RETURNS @t TABLE
(
	 LOAN_ID BIGINT
	,PROPERTY_ID BIGINT
	,REQUIRED_COVERAGE_ID BIGINT
)


AS
BEGIN
	DECLARE @LENDER_LIST TABLE
	(
		ID BIGINT
	)

	DECLARE @Previous7Days Date = DATEADD(DAY, -7, GETDATE());

	INSERT INTO @LENDER_LIST
	SELECT * FROM GetSelectedLenderIds(@lenderIds)
	ORDER BY ID ASC;



	WITH NONCONDO_INADEQUATE_COV AS 
	(
		SELECT DISTINCT
			L.ID AS [LOAN_ID]
			,P.ID AS [PROPERTY_ID]
			,RC.ID AS [REQUIRED_COVERAGE_ID]
			,LE.ID AS [LENDER_ID]
			,RC.SUMMARY_STATUS_CD
			,RC.SUMMARY_SUB_STATUS_CD
			,I.CREATE_DT AS [IMPAIRMENT_DT]
			,I.CODE_CD
		FROM LENDER AS LE 
			INNER JOIN @LENDER_LIST LL ON LL.ID = LE.ID
			INNER JOIN LOAN AS L ON L.LENDER_ID = LE.ID
			INNER JOIN COLLATERAL AS C ON C.LOAN_ID = L.ID 
			INNER JOIN PROPERTY AS P ON P.ID = C.PROPERTY_ID
			INNER JOIN REQUIRED_COVERAGE AS RC ON RC.PROPERTY_ID = P.ID 
			INNER JOIN LENDER_PRODUCT AS LP ON LP.ID = RC.LENDER_PRODUCT_ID 
			INNER JOIN COLLATERAL_CODE AS CC ON CC.ID = C.COLLATERAL_CODE_ID
			INNER JOIN IMPAIRMENT AS I ON I.REQUIRED_COVERAGE_ID = RC.ID
		WHERE	
			LE.STATUS_CD IN ('ACTIVE', 'AUDIT')
			AND	LE.TEST_IN = 'N'
			AND L.RECORD_TYPE_CD = 'G'
			AND	L.STATUS_CD != 'U'
			AND L.EXTRACT_UNMATCH_COUNT_NO = 0
			AND	C.EXTRACT_UNMATCH_COUNT_NO = 0
			AND C.STATUS_CD != 'U'
			AND C.PRIMARY_LOAN_IN = 'Y'
			AND CC.SECONDARY_CLASS_CD NOT IN ('COND')
			AND P.RECORD_TYPE_CD = 'G'
			AND RC.SUMMARY_STATUS_CD IN ('F', 'P')
			AND RC.SUMMARY_SUB_STATUS_CD = 'I'
			AND RC.STATUS_CD != 'I'
			AND I.CODE_CD = 'IC'
			AND I.CREATE_DT >= @Previous7Days
			AND	I.CURRENT_IMPAIRED_IN = 'Y'
			AND LP.END_DT > GETDATE()
	),
	
	--FOR NON-CONDO
	--THE OLDEST POLICY EXPIRATION DATE BETWEEN CUO AND CA POLICIES WHICH SHOULD BE THE CPI QUOTE START DATE
	--IF CC.CODE_TX = PUD/PUDE AND BASE PROPERTY/POLICY TYPE = CA/CUO PULL IN
	--IF CC.CODE_TX = CONDO/CONDOE AND BASE PROPERTY/POLICY TYPE = REAL ESTATE 
	--IF PROPERTY_ASSOCIATION.UNITS_NO < 5
	CONDO_INADEQUATE_COV AS
	(
		SELECT 	
			L.ID AS [LOAN_ID]			
			,RC.PROPERTY_ID
			,RC.ID AS [REQUIRED_COVERAGE_ID]
			,L.LENDER_ID
			,I.CREATE_DT AS [IMPAIRMENT_DT]
			,(
				SELECT MIN(OP_SUB.EXPIRATION_DT) 
				FROM REQUIRED_COVERAGE RC_SUB
					CROSS APPLY GetCurrentCoverage(RC_SUB.PROPERTY_ID, RC_SUB.ID, RC_SUB.TYPE_CD) OP_SUB 
				WHERE 
					RC_SUB.ID = RC.ID					
			) AS [CORRECT_CPIQUOTE_START_DT]
			,FPC.EFFECTIVE_DT
			,CC.CODE_TX
			,OP.BASE_PROPERTY_TYPE_CD
			,RC.SUMMARY_STATUS_CD
			,RC.SUMMARY_SUB_STATUS_CD
		FROM LOAN L 
			INNER JOIN LENDER LE ON LE.ID = L.LENDER_ID
			INNER JOIN @LENDER_LIST LL ON LL.ID = LE.ID
			INNER JOIN COLLATERAL C ON C.LOAN_ID = L.ID
			INNER JOIN PROPERTY P ON P.ID = C.PROPERTY_ID
			INNER JOIN COLLATERAL_CODE CC ON CC.ID = C.COLLATERAL_CODE_ID
			INNER JOIN REQUIRED_COVERAGE RC ON RC.PROPERTY_ID = C.PROPERTY_ID
			INNER JOIN LENDER_PRODUCT LP ON LP.ID = RC.LENDER_PRODUCT_ID
			INNER JOIN IMPAIRMENT I ON I.REQUIRED_COVERAGE_ID = RC.ID
			INNER JOIN FORCE_PLACED_CERT_REQUIRED_COVERAGE_RELATE FPCRCR ON FPCRCR.REQUIRED_COVERAGE_ID = RC.ID
			INNER JOIN FORCE_PLACED_CERTIFICATE FPC ON FPC.ID = FPCRCR.FPC_ID
			INNER JOIN CPI_QUOTE CQ ON CQ.ID = FPC.CPI_QUOTE_ID
			CROSS APPLY GetCurrentCoverage(RC.PROPERTY_ID, RC.ID, RC.TYPE_CD) OP
			LEFT JOIN PROPERTY_ASSOCIATION PA ON PA.ID = P.PROPERTY_ASSOC_ID
		WHERE 
			LE.STATUS_CD IN ('ACTIVE', 'AUDIT')
			AND	LE.TEST_IN = 'N'
			AND L.RECORD_TYPE_CD = 'G'
			AND	L.STATUS_CD != 'U'
			AND L.EXTRACT_UNMATCH_COUNT_NO = 0
			AND	C.EXTRACT_UNMATCH_COUNT_NO = 0
			AND C.STATUS_CD != 'U'
			AND C.PRIMARY_LOAN_IN = 'Y'
			AND CC.SECONDARY_CLASS_CD IN ('COND')
			AND P.RECORD_TYPE_CD = 'G'
			AND RC.SUMMARY_STATUS_CD IN ('F', 'P')
			AND RC.SUMMARY_SUB_STATUS_CD = 'I'
			AND RC.STATUS_CD != 'I'
			AND I.CODE_CD = 'IC'
			AND I.CREATE_DT >= @Previous7Days
			AND	I.CURRENT_IMPAIRED_IN = 'Y'			
			AND LP.END_DT > GETDATE()
		GROUP BY	
			L.ID
			,RC.PROPERTY_ID
			,RC.ID
			,FPC.EFFECTIVE_DT
			,CC.CODE_TX
			,OP.BASE_PROPERTY_TYPE_CD
			,RC.SUMMARY_STATUS_CD
			,RC.SUMMARY_SUB_STATUS_CD
			,L.LENDER_ID
			,I.CREATE_DT
	),	

	CONDOS_INCORRECT_QUOTE AS
	(
		SELECT 
			CIC.LOAN_ID
			,CIC.PROPERTY_ID
			,CIC.REQUIRED_COVERAGE_ID
			,CIC.SUMMARY_STATUS_CD
			,CIC.SUMMARY_SUB_STATUS_CD
			,CIC.LENDER_ID
			,CIC.IMPAIRMENT_DT
		FROM CONDO_INADEQUATE_COV CIC
		WHERE 
			CIC.[CORRECT_CPIQUOTE_START_DT] != CIC.EFFECTIVE_DT
	),

	CONDOS_INCORRECT_POLICY AS
	(
		SELECT
			CIC.LOAN_ID
			,CIC.PROPERTY_ID
			,CIC.REQUIRED_COVERAGE_ID
			,CIC.SUMMARY_STATUS_CD
			,CIC.SUMMARY_SUB_STATUS_CD
			,CIC.LENDER_ID
			,CIC.IMPAIRMENT_DT
		FROM CONDO_INADEQUATE_COV CIC
		WHERE 
			(CIC.CODE_TX IN ('PUD', 'PUDE') AND CIC.BASE_PROPERTY_TYPE_CD IN ('CA', 'CUO'))
			OR (CIC.CODE_TX IN ('CONDO', 'CONDOE') AND CIC.BASE_PROPERTY_TYPE_CD = 'RE')
	),

	Properties_Inadequate_Coverage AS 
	(
		SELECT 
			LOAN_ID
			,PROPERTY_ID
			,REQUIRED_COVERAGE_ID
			,SUMMARY_STATUS_CD
			,SUMMARY_SUB_STATUS_CD
			,LENDER_ID
			,IMPAIRMENT_DT
		FROM NONCONDO_INADEQUATE_COV
		UNION 
		SELECT 
			LOAN_ID
			,PROPERTY_ID
			,REQUIRED_COVERAGE_ID
			,SUMMARY_STATUS_CD
			,SUMMARY_SUB_STATUS_CD
			,LENDER_ID
			,IMPAIRMENT_DT
		FROM CONDOS_INCORRECT_QUOTE
		UNION 
		SELECT 
			LOAN_ID
			,PROPERTY_ID
			,REQUIRED_COVERAGE_ID
			,SUMMARY_STATUS_CD
			,SUMMARY_SUB_STATUS_CD
			,LENDER_ID
			,IMPAIRMENT_DT
		FROM CONDOS_INCORRECT_POLICY
	),

	Notices_Generated AS
	(
		SELECT DISTINCT
			 PIC.*
			,N.ID AS [NOTICE_ID]
			,N.CREATE_DT AS [NOTICE_CREATE_DT]
			,N.TYPE_CD AS [NOTICE_TYPE]
			,N.CAPTURED_DATA_XML.value('(/CapturedData/Coverage/@SummaryStatus)[1]', 'varchar(10)') AS [NOTICE_SUMMARY_STATUS_CD]
			,N.CAPTURED_DATA_XML.value('(/CapturedData/Coverage/@SummarySubStatus)[1]', 'varchar(10)') AS [NOTICE_SUMMARY_SUB_STATUS_CD]
		FROM Properties_Inadequate_Coverage AS PIC
			INNER JOIN NOTICE_REQUIRED_COVERAGE_RELATE AS NRCR ON NRCR.REQUIRED_COVERAGE_ID = PIC.REQUIRED_COVERAGE_ID
			INNER JOIN NOTICE AS N ON N.ID = NRCR.NOTICE_ID
	),

	Notices_Matched AS
	(
		SELECT 
			 LOAN_ID 
			,PROPERTY_ID 
			,REQUIRED_COVERAGE_ID 
			,LENDER_ID 
			,SUMMARY_STATUS_CD 
			,SUMMARY_SUB_STATUS_CD 
			,IMPAIRMENT_DT
			,NOTICE_ID
		FROM Notices_Generated AS NG
		WHERE 
			NG.NOTICE_CREATE_DT >= NG.IMPAIRMENT_DT
			AND	NG.NOTICE_SUMMARY_STATUS_CD IN ('F', 'P')
			AND	NG.SUMMARY_SUB_STATUS_CD = 'I'
			AND	NG.NOTICE_TYPE IN ('I', 'IC')
	),

	Notices_Printed AS
	(
		SELECT DISTINCT
			NM.*
		FROM Notices_Matched AS NM
			INNER JOIN DOCUMENT_CONTAINER AS DC ON DC.RELATE_ID = NOTICE_ID 
		WHERE 
			DC.RELATE_CLASS_NAME_TX = 'Allied.UniTrac.Notice' 
			AND	DC.PRINT_STATUS_CD = 'PRINTED'
			AND	DC.PURGE_DT IS NULL
	), 

	Gross_List AS
	(
		SELECT 
			 PIC.*
		FROM Properties_Inadequate_Coverage AS PIC
		WHERE NOT EXISTS (SELECT REQUIRED_COVERAGE_ID FROM Notices_Printed AS NP WHERE NP.REQUIRED_COVERAGE_ID = PIC.REQUIRED_COVERAGE_ID)
	),

	Already_Generated AS
	(
		SELECT 
			QCI.REQUIRED_COVERAGE_ID
		FROM Gross_List AS GL
			INNER JOIN QUALITY_CONTROL_ITEM AS QCI ON QCI.PROPERTY_ID = GL.PROPERTY_ID AND QCI.REQUIRED_COVERAGE_ID = GL.REQUIRED_COVERAGE_ID
		WHERE 
			QCI.QC_RULE_DEF_CD = 'INADCOV'
			AND	QCI.PURGE_DT IS NULL
			AND QCI.CREATE_DT >= GL.IMPAIRMENT_DT
	)

	INSERT INTO @t
	SELECT 
		 GL.LOAN_ID 
		,GL.PROPERTY_ID
		,GL.REQUIRED_COVERAGE_ID
	FROM Gross_List AS GL
	WHERE 
		NOT EXISTS (SELECT REQUIRED_COVERAGE_ID FROM Already_Generated AS AG WHERE AG.REQUIRED_COVERAGE_ID = GL.REQUIRED_COVERAGE_ID)
	GROUP BY
		GL.LOAN_ID
		,GL.PROPERTY_ID
		,GL.REQUIRED_COVERAGE_ID

	RETURN
END
GO

