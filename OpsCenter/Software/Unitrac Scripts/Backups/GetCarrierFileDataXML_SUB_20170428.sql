USE [Unitrac_Reports]
GO

/****** Object:  StoredProcedure [dbo].[GetCarrierFileDataXML]    Script Date: 4/28/2017 2:49:01 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



CREATE PROCEDURE [dbo].[GetCarrierFileDataXML] (@AccountingPeriodId bigint,
@CarrierCode nvarchar(20))
AS
BEGIN

	--Getting these Values From the VUT Agency Server, Still need to confirm do we really need Batch Number
	--CAS	GC9
	--CET	GC5
	--CML	GC8
	--CST	GC2
	--EST	GC1
	--MID	GC7
	--MST	GC4
	--PST	GC6
	--WES	GC3
	CREATE TABLE #BATCH_NUMBER (
		REGION_TX varchar(10),
		BATCH_NUM_TX varchar(10)
	)

	INSERT INTO #BATCH_NUMBER
		SELECT
			'CAS',
			'GC9'
		UNION
		SELECT
			'CET',
			'GC5'
		UNION
		SELECT
			'CML',
			'GC8'
		UNION
		SELECT
			'CST',
			'GC2'
		UNION
		SELECT
			'EST',
			'GC1'
		UNION
		SELECT
			'MID',
			'GC7'
		UNION
		SELECT
			'MST',
			'GC4'
		UNION
		SELECT
			'PST',
			'GC6'
		UNION
		SELECT
			'WES',
			'GC3'

	SELECT
		--AGENCY
		A.ID AS AGENCY_ID,
		A.SHORT_NAME_TX AS AGENCY_SHORT_NAME,
		--ACCOUNTING PERIOD
		ISNULL(AP.STATEMENT_DT, '') AS ACCT_PERIOD_ST_DATE,
      ISNULL(AP.END_DT ,'') AS ACCT_PERIOD_END_DT ,
		--CARRIER
		ISNULL(C.CODE_TX, '') AS CARRIER_CODE_TX,
		ISNULL(C.NAME_TX, '') AS CARRIER_NAME_TX,
		--- CARRIER_PRODUCT
		CP.ID AS CP_ID,
		ISNULL(CP.EXTERNAL_CARRIER_PRODUCT_CD, '') AS EXTERNAL_CARRIER_PRODUCT_CD,
		ISNULL(CP.CARRIER_PRODUCT_CD, '') AS CARRIER_PRODUCT_CD,
		---MASTER_POLICY
		MP.ID AS MP_ID,
		ISNULL(MP.NUMBER_TX, '') AS MP_NUMBER_TX,
		ISNULL(MP.ID_NUMBER_TX, '') AS MP_ID_NUMBER_TX,
      ---MASTER_POLICY ASSIGNMENT
		ISNULL(MPA.SPECIAL_HANDLING_XML.value('(/SH/BorrowerMtgDeductible)[1]', 'NVARCHAR(50)'), '') AS MP_BORR_MORTG_DEDUCTIBLE,
      ISNULL(MPA.SPECIAL_HANDLING_XML.value('(/SH/BorrowerComprehensiveDeductible)[1]', 'NVARCHAR(50)'), '') AS MP_BORR_VEH_COMP_DEDUCTIBLE,
		--- FPC
		FPC.ID AS FPC_ID,
		ISNULL(FPC.NUMBER_TX, '') AS FPC_NUMBER_TX,
		ISNULL(FPC.EFFECTIVE_DT, '') AS FPC_EFFECTIVE_DT,
		ISNULL(FPC.CANCELLATION_DT, '') AS FPC_CANCELLATION_DT,
		FPC.MONTHLY_BILLING_IN AS FPC_MONTHLY_BILLING_IN,
		ISNULL(FPC.EXPIRATION_DT, '') AS FPC_EXPIRATION_DT,
		ISNULL(FPC.PRODUCER_NUMBER_TX, '') AS FPC_PRODUCER_NUMBER_TX,
		ISNULL(FPC.ISSUE_DT, '') AS FPC_ISSUE_DT,
		ISNULL(FPC.STATUS_CD, '') AS FPC_STATUS_CD,
		---CPI_QUOTE
		ISNULL(CQ.BASIS_NO, 0) AS CPI_QUOTE_BASIS_NO,
		ISNULL(CQ.TERM_TYPE_CD, '') AS CPIQ_TERM_TYPE_CD,
      ISNULL(CQ.TERM_NO,0) AS CPI_TERM_NO,		
		--CPI_ACTIVITY
		ISNULL(CPA_CANCEL.CANCEL_EVENT_DT, '') AS PREV_CANCEL_DT,
		ISNULL(CPIA_ISSUE_AMT.ISSUE_AMT, 0) AS CPIA_ISSUE_AMT,
		ISNULL(CPIA_CANCEL_REIN_AMT.CANCEL_REI_AMT, 0) AS CPIA_CANCEL_REI_AMT,
      ISNULL(CPA_CANCEL_RSN.CANCEL_REASON,'') as CPI_CANCEL_REASON,
      ISNULL(CPA_CANCEL_RSN.CANCEL_REASON_CD,'') as CPI_CANCEL_REASON_CD,
		---FIN_RPT_TXN
		FRT.TYPE_CD AS FRT_TYPE_CD,
		ISNULL(FRT.TERM_NO, 0) AS FRT_TERM_NO,
		ISNULL(FRT.SUB_TYPE_CD, '') AS FRT_SUB_TYPE_CD,
		ISNULL(FRT.EFFECTIVE_DT, '') AS FRT_EFFECTIVE_DT,
		ISNULL(FRT.PROCESSED_DT, '') AS FRT_PROCESSED_DT,
		ISNULL(FRT.MONTH_NO, 0) AS FRT_MONTH_NO,
		ISNULL(FRT.FULL_TERM_AMOUNT_NO, 0) AS FRT_FULL_TERM_AMOUNT_NO,
		---FIN_RPT_TXN_DET
		ISNULL(FRTD_WRITTEN_PREM.AMOUNT_NO, 0) AS WRITTEN_PREMIUM_NO,
		ISNULL(FRTD_FEE.AMOUNT_NO, 0) AS FEE_NO,
		ISNULL(FRTD_TAX1.AMOUNT_NO, 0) AS TAX1_NO,
		ISNULL(FRTD_TAX2.AMOUNT_NO, 0) AS TAX2_NO,
		ISNULL(FRTD_TAX3.AMOUNT_NO, 0) AS TAX3_NO,
		ISNULL(FRTD_TAX4.AMOUNT_NO, 0) AS TAX4_NO,
		ISNULL(FRTD_OTHER.AMOUNT_NO, 0) AS OTHER_NO,
		--COMMISSION
		ISNULL(CR.PERCENT_NO, 0) AS CMSN_PREC_NO,   
      --PREV ISSUE 
      ISNULL(ISSUE_NEW_UPDATE.ISSUE_CNT , 0 ) AS ISSUE_NEW_UPDATE_CNT , 
		--LENDER
		L.ID AS LENDER_ID,
		L.CODE_TX AS LENDER_CODE_TX,
		L.NAME_TX AS LENDER_NAME_TX,
		'' AS LENDER_SPECIAL_CODE, --To Do: Special Code From related Data.
		ISNULL(LENDER_MAILING_ADDRESS.LINE_1_TX, '') AS LENDER_MAILING_ADD_LINE1,
		ISNULL(LENDER_MAILING_ADDRESS.LINE_2_TX, '') AS LENDER_MAILING_ADD_LINE2,
		ISNULL(LENDER_MAILING_ADDRESS.STATE_PROV_TX, '') AS LENDER_MAILING_ADD_STATE_TX,
		ISNULL(LDR_MAILING_ADD_STATE_CODE.VALUE_TX, '') AS LENDER_MAILING_ADD_STATE_CODE,
		ISNULL(LENDER_MAILING_ADDRESS.CITY_TX, '') AS LENDER_MAILING_ADD_CITY_TX,
		ISNULL(LENDER_MAILING_ADDRESS.POSTAL_CODE_TX, '') AS LENDER_MAILING_ADD_POSTAL_CODE_TX,

		ISNULL(LENDER_PHY_ADDRESS.LINE_1_TX, '') AS LENDER_PHY_ADDRESS_LINE1,
		ISNULL(LENDER_PHY_ADDRESS.LINE_2_TX, '') AS LENDER_PHY_ADDRESS_LINE2,
		ISNULL(LENDER_PHY_ADDRESS.STATE_PROV_TX, '') AS LENDER_PHY_ADDRESS_STATE_TX,
		ISNULL(LDR_PHYSICAL_ADD_STATE_CODE.VALUE_TX, '') AS LENDER_PHYSICAL_ADD_STATE_CODE,
		ISNULL(LENDER_PHY_ADDRESS.CITY_TX, '') AS LENDER_PHY_ADDRESS_CITY_TX,
		ISNULL(LENDER_PHY_ADDRESS.POSTAL_CODE_TX, '') AS LENDER_PHY_ADDRESS_POSTAL_CODE_TX,
		ISNULL((SELECT
			dbo.[VUTAgencyGetlkuLenderMHNumber](L.CODE_TX, A.SHORT_NAME_TX))
		, '') AS LENDER_MH_NUMBER,
		--LOAN
		LN.ID AS LOAN_ID,
		ISNULL(LN.NUMBER_TX, '') AS LOAN_NUMBER_TX,
		ISNULL(LN.DIVISION_CODE_TX, '') AS LOAN_DIVISION_CD_TX,
		ISNULL(LN.BRANCH_CODE_TX, '') AS LOAN_BRANCH_CODE_TX,
      ISNULL(LN.TYPE_CD, '') AS LOAN_TYPE_CD, 
		--COLLATERAL
		ISNULL(CC.CODE_TX, '') AS COLL_CODE_TX,
		ISNULL(CC.PRIMARY_CLASS_CD, '') AS COLL_CODE_PRIMARY_CLASS_CD,
		ISNULL(CC.SECONDARY_CLASS_CD, '') AS COLL_CODE_SEC_CLASSIFICATION_CD,
		ISNULL(PROPERTY_TYPE_RCA.VALUE_TX, '') AS PROPERTY_TYPE,
		ISNULL(COLL.COLLATERAL_NUMBER_NO, 0) AS COLL_NUMBER_NO,
		ISNULL(COLL.LOAN_BALANCE_NO, 0) AS COLL_LOAN_BALANCE_NO,
		---PROPERTY DESC
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/@VIN)[1]' , 'nvarchar(18)'),'') AS PROP_VIN_TX ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/@make)[1]' , 'nvarchar(50)'),'') AS PROP_MAKE_TX ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/@model)[1]' , 'nvarchar(50)'),'') AS PROP_MODEL_TX ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/@body)[1]' , 'nvarchar(50)'),'') AS PROP_BODY_TX ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/@year)[1]' , 'nvarchar(4)'),'') AS PROP_YEAR_TX ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/Address/@Line1)[1]' , 'nvarchar(100)'),'') AS PROP_ADD_LINE_1 ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/Address/@Line2)[1]' , 'nvarchar(100)'),'') AS PROP_ADD_LINE_2 ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/Address/@City)[1]' , 'nvarchar(50)'),'') AS PROP_ADD_CITY_TX ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/Address/@State)[1]' , 'nvarchar(30)'),'') AS PROP_ADD_STATE_TX ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/Address/@PostalCode)[1]' , 'nvarchar(30)'),'') AS PROP_ADD_ZIP_TX ,
		ISNULL(FPC.CAPTURED_DATA_XML.value('(/CapturedData/Property/@description)[1]' , 'nvarchar(100)'),'') AS PROP_DESCRIPTION_TX ,
      ---PROPERTY 
		ISNULL(P.FLOOD_ZONE_TX, '') AS PROP_FLOOD_ZONE_TX,
		ISNULL(P.ACV_NO, 0) AS ACV_NO,
      ISNULL(P.REPLACEMENT_COST_VALUE_NO, 0) AS RCV_NO,
		PROPERTY_CODE = CASE WHEN A.SHORT_NAME_TX = 'ALLIED' THEN
		                   ISNULL((SELECT dbo.VUTAgencyGetPropertyCode(C.VUT_KEY, CC.VUT_KEY, CAST(CC.CONTRACT_TYPE_CD AS int), RC.TYPE_CD, A.SHORT_NAME_TX)), '') 
			             ELSE ISNULL((SELECT dbo.VUTAgencyGetPropertyCode(C.ID, CC.ID, CAST(CC.CONTRACT_TYPE_CD AS int), RC.TYPE_CD, A.SHORT_NAME_TX)), '') END,
		PROPERTY_TYPE_CODE = CASE WHEN  A.SHORT_NAME_TX = 'ALLIED' THEN
							         ISNULL((SELECT dbo.VUTAgencyGetPropertyTypeCode(C.VUT_KEY, CC.VUT_KEY, CAST(CC.CONTRACT_TYPE_CD AS int), RC.TYPE_CD, A.SHORT_NAME_TX)), '') 
							      ELSE ISNULL((SELECT dbo.VUTAgencyGetPropertyTypeCode(C.ID, CC.ID, CAST(CC.CONTRACT_TYPE_CD AS int), RC.TYPE_CD, A.SHORT_NAME_TX)), '')  END,
      CASE WHEN C.CODE_TX = '5679' THEN 
			ISNULL( RATEPROPCD.RATEPROPERTYCODE , '') ELSE '' END AS RATE_PROPERTY_CODE,		
      ISNULL((SELECT dbo.GetCarrierCompanyCode(PROP_ADD.STATE_PROV_TX, C.CODE_TX)), '') AS COMPANY_CODE_TX,
      ISNULL(PROP_STATE_CODE.VALUE_TX ,'') AS PROP_STATE_CODE_TX, 
		--REQUIRED COVERAGE
		ISNULL(RC.TYPE_CD, '') AS COVERAGE_TYPE_CD,
		---OWNER
		ISNULL(O.LAST_NAME_TX, '') AS OWNER_LAST_NAME_TX,
		ISNULL(O.FIRST_NAME_TX, '') AS OWNER_FIRST_NAME_TX,
		ISNULL(O.MIDDLE_INITIAL_TX, '') AS OWNER_MIDDLE_INITIAL_TX,
		ISNULL(OA.LINE_1_TX, '') AS OWNER_ADD_LINE_1,
		ISNULL(OA.LINE_2_TX, '') AS OWNER_ADD_LINE_2,
		ISNULL(OA.CITY_TX, '') AS OWNER_ADD_CITY_TX,
		ISNULL(OA.STATE_PROV_TX, '') AS OWNER_ADD_STATE_TX,
		ISNULL(OA.POSTAL_CODE_TX, '') AS OWNER_ADD_ZIP_TX,
		--Branch Details
		LO_BRANCH.ID AS LO_BRANCH_ID,
		ISNULL(LO_BRANCH.CODE_TX, '') AS LO_BRANCH_CODE_TX,
		ISNULL(LO_BRANCH.NAME_TX, '') AS LO_BRANCH_NAME_TX,
		ISNULL(BRANCH_MAILING_ADD.LINE_1_TX, '') AS BRANCH_MAILING_ADD_LINE1,
		ISNULL(BRANCH_MAILING_ADD.LINE_2_TX, '') AS BRANCH_MAILING_ADD_LINE2,
		ISNULL(BRANCH_MAILING_ADD.STATE_PROV_TX, '') AS BRANCH_MAILING_ADD_STATE_TX,
		ISNULL(BRCH_MAILING_STATE_CODE.VALUE_TX, '') AS BRANCH_MAILING_STATE_CODE,
		ISNULL(BRANCH_MAILING_ADD.CITY_TX, '') AS BRANCH_MAILING_ADD_CITY_TX,
		ISNULL(BRANCH_MAILING_ADD.POSTAL_CODE_TX, '') AS BRANCH_MAILING_ADD_POSTAL_CODE_TX,
		ISNULL(BRANCH_PHYSICAL_ADD.LINE_1_TX, '') AS BRANCH_PHYSICAL_ADD_LINE1,
		ISNULL(BRANCH_PHYSICAL_ADD.LINE_2_TX, '') AS BRANCH_PHYSICAL_ADD_LINE2,
		ISNULL(BRANCH_PHYSICAL_ADD.STATE_PROV_TX, '') AS BRANCH_PHYSICAL_ADD_STATE_TX,
		ISNULL(BRANCH_PHYSICAL_ADD.CITY_TX, '') AS BRANCH_PHYSICAL_ADD_CITY_TX,
		ISNULL(BRANCH_PHYSICAL_ADD.POSTAL_CODE_TX, '') AS BRANCH_PHYSICAL_ADD_POSTAL_CODE_TX,
		ISNULL(BRCH_PHYSICAL_STATE_CODE.VALUE_TX, '') AS BRANCH_PHYSICAL_STATE_CODE,
		--Division Details
		LO_DIVISION.ID AS LO_DIV_ID,
		LO_DIVISION.CODE_TX AS LO_DIV_CODE_TX,
		LO_DIVISION.NAME_TX AS LO_DIV_NAME_TX,
		(SELECT
			BN.BATCH_NUM_TX
		FROM #BATCH_NUMBER BN
		WHERE BN.REGION_TX = LENDER_REGION.VUT_REGION)
		AS BATCH_NUMBER_TX ,      
      ---- CALC ISS DETAILS
		ISNULL(CPI_ISS_PREM.AMOUNT_NO,0) AS CALCISSPREM_NO ,      
		---- CALC CAN DETAILS
		ISNULL(CPI_CXL_PREM.AMOUNT_NO,0) AS CALCXLPREM_NO       
	FROM ACCOUNTING_PERIOD AP
	JOIN AGENCY A
		ON AP.AGENCY_ID = A.ID
	JOIN FINANCIAL_REPORT_TXN FRT
		ON AP.ID = FRT.ACCOUNTING_PERIOD_ID
	JOIN FORCE_PLACED_CERTIFICATE FPC
		ON FRT.FPC_ID = FPC.ID
	LEFT JOIN FORCE_PLACED_CERT_REQUIRED_COVERAGE_RELATE FPCRCR
		ON FPC.ID = FPCRCR.FPC_ID AND FPCRCR.PURGE_DT IS NULL
	LEFT JOIN REQUIRED_COVERAGE RC
		ON FPCRCR.REQUIRED_COVERAGE_ID = RC.ID
	LEFT JOIN FINANCIAL_REPORT_TXN_DETAIL FRTD_WRITTEN_PREM
		ON (FRTD_WRITTEN_PREM.TYPE_CD = 'PRM' AND FRT.ID = FRTD_WRITTEN_PREM.FRT_ID)
	LEFT JOIN FINANCIAL_REPORT_TXN_DETAIL FRTD_FEE
		ON (FRTD_FEE.TYPE_CD = 'FEE' AND FRT.ID = FRTD_FEE.FRT_ID)
	LEFT JOIN FINANCIAL_REPORT_TXN_DETAIL FRTD_TAX1
		ON (FRTD_TAX1.TYPE_CD = 'TAX1' AND FRT.ID = FRTD_TAX1.FRT_ID)
	LEFT JOIN FINANCIAL_REPORT_TXN_DETAIL FRTD_TAX2
		ON (FRTD_TAX2.TYPE_CD = 'TAX2' AND FRT.ID = FRTD_TAX2.FRT_ID)
	LEFT JOIN FINANCIAL_REPORT_TXN_DETAIL FRTD_TAX3
		ON (FRTD_TAX3.TYPE_CD = 'TAX3' AND FRT.ID = FRTD_TAX3.FRT_ID)
	LEFT JOIN FINANCIAL_REPORT_TXN_DETAIL FRTD_TAX4
		ON (FRTD_TAX4.TYPE_CD = 'TAX4' AND FRT.ID = FRTD_TAX4.FRT_ID)
	LEFT JOIN FINANCIAL_REPORT_TXN_DETAIL FRTD_OTHER
		ON (FRTD_OTHER.TYPE_CD = 'OTH' AND FRT.ID = FRTD_OTHER.FRT_ID)
	JOIN CPI_QUOTE CQ
		ON FPC.CPI_QUOTE_ID = CQ.ID
	JOIN Loan LN
		ON FPC.LOAN_ID = LN.ID
	JOIN PROPERTY P
		ON RC.PROPERTY_ID = P.ID
	JOIN Collateral COLL
		ON COLL.PROPERTY_ID = P.ID AND COLL.PRIMARY_LOAN_IN = 'Y'
      AND COLL.PURGE_DT IS NULL
   --CROSS APPLY (SELECT TOP 1 C1.LOAN_BALANCE_NO, C1.COLLATERAL_NUMBER_NO, C1.COLLATERAL_CODE_ID
   --          FROM COLLATERAL C1
   --          WHERE P.ID = C1.PROPERTY_ID AND C1.PURGE_DT IS NULL AND C1.PRIMARY_LOAN_IN = 'Y' ORDER BY C1.CREATE_DT DESC) AS COLL 
	JOIN OWNER_LOAN_RELATE OLR
		ON (LN.ID = OLR.LOAN_ID AND OLR.PRIMARY_IN = 'Y' AND OLR.PURGE_DT IS NULL)
	JOIN OWNER O
		ON OLR.OWNER_ID = O.ID
	LEFT JOIN OWNER_ADDRESS OA
		ON OA.ID = O.ADDRESS_ID
	JOIN LENDER L
		ON LN.LENDER_ID = L.ID
	LEFT JOIN ADDRESS LENDER_PHY_ADDRESS
		ON L.PHYSICAL_ADDRESS_ID = LENDER_PHY_ADDRESS.ID
	LEFT JOIN ADDRESS LENDER_MAILING_ADDRESS
		ON L.MAILING_ADDRESS_ID = LENDER_MAILING_ADDRESS.ID
	JOIN CARRIER C
		ON C.CODE_TX = @CarrierCode
	JOIN MASTER_POLICY MP
		ON (FPC.MASTER_POLICY_ID = MP.ID AND C.ID = MP.CARRIER_ID)
	LEFT JOIN MASTER_POLICY_ASSIGNMENT MPA
		ON FPC.MASTER_POLICY_ASSIGNMENT_ID = MPA.ID
	JOIN CARRIER_PRODUCT CP
		ON CP.ID = MP.CARRIER_PRODUCT_ID
	LEFT JOIN COMMISSION CMSN
		ON FRT.ID = CMSN.FRT_ID AND CMSN.TYPE_CD = 'AGENCY'
	LEFT JOIN COMMISSION_RATE CR
		ON CMSN.COMMISSION_RATE_ID = CR.ID
	LEFT JOIN OWNER_ADDRESS PROP_ADD
		ON P.ADDRESS_ID = PROP_ADD.ID
	--Branch
	LEFT JOIN LENDER_ORGANIZATION LO_BRANCH
		ON (LO_BRANCH.LENDER_ID = LN.LENDER_ID AND LO_BRANCH.CODE_TX = LN.BRANCH_CODE_TX AND LO_BRANCH.TYPE_CD = 'BRCH' AND LO_BRANCH.PURGE_DT IS NULL)
	LEFT JOIN ADDRESS BRANCH_MAILING_ADD
		ON LO_BRANCH.MAILING_ADDRESS_ID = BRANCH_MAILING_ADD.ID
	LEFT JOIN ADDRESS BRANCH_PHYSICAL_ADD
		ON LO_BRANCH.PHYSICAL_ADDRESS_ID = BRANCH_PHYSICAL_ADD.ID
	--Division
	LEFT JOIN LENDER_ORGANIZATION LO_DIVISION
		ON (LO_DIVISION.LENDER_ID = LN.LENDER_ID AND LO_DIVISION.CODE_TX = LN.DIVISION_CODE_TX AND LO_DIVISION.TYPE_CD = 'DIV' AND LO_DIVISION.PURGE_DT IS NULL)
	--Collatral Code
	LEFT JOIN COLLATERAL_CODE CC
		ON COLL.COLLATERAL_CODE_ID = CC.ID
	--Prev Cancellation Date
	OUTER APPLY (SELECT
		MAX(PROCESS_DT) AS CANCEL_EVENT_DT
	FROM CPI_ACTIVITY CPA
	WHERE TYPE_CD IN ('C', 'MT') AND
	CPA.CPI_QUOTE_ID = CQ.ID)
	AS CPA_CANCEL
   --Cancel Reason
	OUTER APPLY (SELECT
	      TOP 1 RC.MEANING_TX AS CANCEL_REASON , RC.CODE_CD AS CANCEL_REASON_CD
	FROM CPI_ACTIVITY CPA
	JOIN REF_CODE RC ON RC.CODE_CD = CPA.REASON_CD 
	AND RC.DOMAIN_CD = 'CANCELREASON'
	WHERE TYPE_CD IN ('C', 'MT') AND
	CPA.CPI_QUOTE_ID = CQ.ID 	
	ORDER BY CPA.PROCESS_DT DESC)
	AS CPA_CANCEL_RSN
   --- New Updates
   OUTER APPLY
     (SELECT COUNT(*) AS ISSUE_CNT FROM 
       FINANCIAL_REPORT_TXN FRTXN
       WHERE FRTXN.FPC_ID = FPC.ID
       AND FRTXN.PURGE_DT IS NULL
       AND FRTXN.ACCOUNTING_PERIOD_ID < @AccountingPeriodId
       AND FRTXN.TYPE_CD IN  ('I' , 'N' , 'R')
       ) AS ISSUE_NEW_UPDATE
	--Property type
	OUTER APPLY (SELECT
		RCA.VALUE_TX
	FROM REF_CODE_ATTRIBUTE RCA
	JOIN REF_CODE RC
		ON (RCA.REF_CD = RC.CODE_CD AND RCA.DOMAIN_CD = RC.DOMAIN_CD)
	WHERE RC.CODE_CD = CC.SECONDARY_CLASS_CD AND RC.DOMAIN_CD = 'SecondaryClassification' AND RCA.ATTRIBUTE_CD = 'PropertyType')
	AS PROPERTY_TYPE_RCA
	--Branch Mailing State Code
	OUTER APPLY (SELECT
		RCA.VALUE_TX
	FROM REF_CODE_ATTRIBUTE RCA
	JOIN ADDRESS BRCH_ADD
		ON (BRCH_ADD.STATE_PROV_TX = RCA.REF_CD)
	WHERE BRCH_ADD.ID = BRANCH_MAILING_ADD.ID AND RCA.ATTRIBUTE_CD = 'StateCode' AND RCA.DOMAIN_CD = 'State')
	AS BRCH_MAILING_STATE_CODE
	--Branch Physical State Code
	OUTER APPLY (SELECT
		RCA.VALUE_TX
	FROM REF_CODE_ATTRIBUTE RCA
	JOIN ADDRESS BRCH_ADD
		ON (BRCH_ADD.STATE_PROV_TX = RCA.REF_CD)
	WHERE BRCH_ADD.ID = BRANCH_PHYSICAL_ADD.ID AND RCA.ATTRIBUTE_CD = 'StateCode' AND RCA.DOMAIN_CD = 'State')
	AS BRCH_PHYSICAL_STATE_CODE
	--Lender Mailing State Code
	OUTER APPLY (SELECT
		RCA.VALUE_TX
	FROM REF_CODE_ATTRIBUTE RCA
	JOIN ADDRESS LDR_ADD
		ON (LDR_ADD.STATE_PROV_TX = RCA.REF_CD)
	WHERE LDR_ADD.ID = LENDER_MAILING_ADDRESS.ID AND RCA.ATTRIBUTE_CD = 'StateCode' AND RCA.DOMAIN_CD = 'State')
	AS LDR_MAILING_ADD_STATE_CODE
	--Lender Physical State Code
	OUTER APPLY (SELECT
		RCA.VALUE_TX
	FROM REF_CODE_ATTRIBUTE RCA
	JOIN ADDRESS LDR_ADD
		ON (LDR_ADD.STATE_PROV_TX = RCA.REF_CD)
	WHERE LDR_ADD.ID = LENDER_PHY_ADDRESS.ID AND RCA.ATTRIBUTE_CD = 'StateCode' AND RCA.DOMAIN_CD = 'State')
	AS LDR_PHYSICAL_ADD_STATE_CODE
   --Property State Code
	OUTER APPLY (SELECT
		RCA.VALUE_TX
	FROM REF_CODE_ATTRIBUTE RCA
	WHERE RCA.REF_CD = PROP_ADD.STATE_PROV_TX 
	AND RCA.ATTRIBUTE_CD = 'StateCode' AND RCA.DOMAIN_CD = 'State')
	AS PROP_STATE_CODE
	--CalcIssttlCharges - CPIActivity - Type - Issue - sum(Amount)
	OUTER APPLY (SELECT
		SUM(CPA.TOTAL_PREMIUM_NO) AS ISSUE_AMT
	FROM CPI_ACTIVITY CPA
	WHERE TYPE_CD = 'I' AND CPA.PURGE_DT IS NULL AND
	CPA.CPI_QUOTE_ID = CQ.ID)
	AS CPIA_ISSUE_AMT
	--CalcCanttlCharges - CPIActivity - Type - Cancel/Midterm cancel/Reinstate - sum(amount)
	OUTER APPLY (SELECT
		SUM(CPA.TOTAL_PREMIUM_NO) AS CANCEL_REI_AMT
	FROM CPI_ACTIVITY CPA
	WHERE TYPE_CD IN ('C', 'MT', 'R') AND CPA.PURGE_DT IS NULL AND
	CPA.CPI_QUOTE_ID = CQ.ID)
	AS CPIA_CANCEL_REIN_AMT   
   ---- CALCISSPREM
	OUTER APPLY (SELECT 
	CD.AMOUNT_NO FROM CERTIFICATE_DETAIL CD 
	JOIN CPI_ACTIVITY ACT ON ACT.ID = CD.CPI_ACTIVITY_ID
	WHERE ACT.CPI_QUOTE_ID = CQ.ID AND CD.TYPE_CD = 'PRM'
	AND ACT.TYPE_CD = 'I' AND ACT.PURGE_DT IS NULL
   AND CD.PURGE_DT IS NULL) AS CPI_ISS_PREM   
	---- CALCCANPREM
	OUTER APPLY (SELECT 
	SUM(CD.AMOUNT_NO) AS AMOUNT_NO FROM CERTIFICATE_DETAIL CD 
	JOIN CPI_ACTIVITY ACT ON ACT.ID = CD.CPI_ACTIVITY_ID
	WHERE ACT.CPI_QUOTE_ID = CQ.ID AND CD.TYPE_CD = 'PRM'
	AND ACT.TYPE_CD IN ('C', 'MT' ,'R') 
   AND ACT.PURGE_DT IS NULL
   AND CD.PURGE_DT IS NULL) AS CPI_CXL_PREM   
   --Lender Region
	OUTER APPLY (SELECT
		VKM.VUT_REGION
	FROM VUT_KEY_MAP VKM (NOLOCK)
	WHERE VKM.RELATE_CLASS_NM = 'Allied.UniTrac.Lender' AND VKM.RELATE_ID = L.ID AND VKM.TYPE_CODE_TX = 'LenderKey')
	AS LENDER_REGION
   OUTER APPLY 
	(
	  SELECT RATEPROPERTYCODE FROM dbo.VUTAgencyGetRateCriteriaPropertyCode(MPA.ISSUE_PROCEDURE_ID, C.VUT_KEY, CC.VUT_KEY, CAST(CC.CONTRACT_TYPE_CD AS int), RC.TYPE_CD, PROP_ADD.STATE_PROV_TX, PROP_ADD.POSTAL_CODE_TX, A.SHORT_NAME_TX, C.CODE_TX)
	) AS RATEPROPCD
	WHERE FRT.ACCOUNTING_PERIOD_ID = @AccountingPeriodId
	AND (AP.PURGE_DT IS NULL AND FRT.PURGE_DT IS NULL AND FPC.PURGE_DT IS NULL AND CMSN.PURGE_DT IS NULL AND CR.PURGE_DT IS NULL)
	FOR xml PATH ('CARRIER_DATA'), ROOT ('CARRIER_DATA_FILES')

END

GO

