USE [UniTrac]
GO

/****** Object:  StoredProcedure [dbo].[GetEscrowBatchDetails]    Script Date: 6/18/2019 4:11:54 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [dbo].[GetEscrowBatchDetails]
(
	 @lenderId bigint,
	 @controlNumber nvarchar(20) = null,
	 @escrowWorkItemId bigint = null
)
AS
BEGIN
	SET NOCOUNT ON
	
	CREATE TABLE #FinalResults(
		ID BIGINT,
		LOAN_NUMBER NVARCHAR(18),
		STATUS_CD NVARCHAR(50),
		STATUS_MEANING NVARCHAR(100),
		GOOD_THRU_DT DATETIME,
		BOR_INS_COMP NVARCHAR(100),
		REMIT_ADD_LINE1 NVARCHAR(100),
		REMIT_ADD_LINE2 NVARCHAR(100),
		REMIT_ADD_CITY NVARCHAR(50),
		REMIT_ADD_STATE NVARCHAR(10),
		PAYEE_CD NVARCHAR(20),
		COVERAGE NVARCHAR(50),
		COVERAGE_STATUS NVARCHAR(50),
		PAYMENT_AMOUNT DECIMAL(18,2),
		PAYMENT_TYPE NVARCHAR(50),
		DUE_DATE DATETIME,
		INTERACTION_DATE DATETIME,
		INTERACTION_TYPE NVARCHAR(100),
		KEY_FIELD NVARCHAR(20),
		KEY_TYPE NVARCHAR(20),
		ESCROW_WORKITEM_ID BIGINT,
		CONTROL_NUMBER NVARCHAR(20),
		ESCROW_VERIFICATION_WORKITEM_ID BIGINT,
		CONTENT_XML xml,
		WORK_ITEM_STATUS_CD NVARCHAR(50),
		LOAN_ID BIGINT,
		PREMIUM_NO DECIMAL(18,2),
		OTHER_NO DECIMAL(18,2),
		DOCUMENT_ID BIGINT
	)

	DECLARE @escrowWorkflowDefId BIGINT
	DECLARE @escrowVerificationWorkflowDefId BIGINT

	SELECT @escrowWorkflowDefId = ID FROM WORKFLOW_DEFINITION WHERE NAME_TX = 'ESCROW' AND PURGE_DT IS NULL
	SELECT @escrowVerificationWorkflowDefId = ID FROM WORKFLOW_DEFINITION WHERE NAME_TX = 'ESCROWVERIFICATION' AND PURGE_DT IS NULL

	CREATE TABLE #escWIResults(
		ESCROW_ID BIGINT,
		ESCROW_WORKITEM_ID BIGINT
	)

	--Get escrows linked to a workitem and open with no control number
	IF @escrowWorkItemId > 0
	BEGIN
		INSERT INTO #escWIResults
		SELECT DISTINCT esc.ID ESCROW_ID, MAX(wi.ID) ESCROW_WORKITEM_ID
		FROM ESCROW esc
			JOIN PROPERTY pr ON pr.ID = esc.PROPERTY_ID AND pr.RECORD_TYPE_CD = 'G' AND pr.PURGE_DT IS NULL
			JOIN REQUIRED_COVERAGE rc ON rc.PROPERTY_ID = pr.ID AND rc.PURGE_DT IS NULL AND rc.RECORD_TYPE_CD = 'G'
			JOIN ESCROW_REQUIRED_COVERAGE_RELATE rel on rel.ESCROW_ID = esc.ID AND rel.REQUIRED_COVERAGE_ID = rc.ID AND rel.PURGE_DT IS NULL
			JOIN REQUIRED_ESCROW re ON re.REQUIRED_COVERAGE_ID = rc.ID AND re.ACTIVE_IN = 'Y' AND re.PURGE_DT IS NULL
			JOIN PROCESS_LOG_ITEM pli ON pli.RELATE_ID = esc.ID AND pli.RELATE_TYPE_CD = 'Allied.Unitrac.Escrow' AND pli.PURGE_DT IS NULL AND pli.STATUS_CD = 'COMP'
			JOIN WORK_ITEM wi ON wi.RELATE_ID = pli.PROCESS_LOG_ID AND wi.WORKFLOW_DEFINITION_ID = @escrowWorkflowDefId AND wi.STATUS_CD NOT IN ('Complete', 'Withdrawn')
		WHERE pr.LENDER_ID = @lenderId 
		AND esc.STATUS_CD = 'OPEN'	
		AND wi.ID = @escrowWorkItemId
		GROUP BY esc.ID
	END
	ELSE 
	BEGIN
		INSERT INTO #escWIResults
		SELECT DISTINCT esc.ID ESCROW_ID, NULL ESCROW_WORKITEM_ID
		FROM ESCROW esc
			JOIN PROPERTY pr ON pr.ID = esc.PROPERTY_ID AND pr.RECORD_TYPE_CD = 'G' AND pr.PURGE_DT IS NULL
			JOIN REQUIRED_COVERAGE rc ON rc.PROPERTY_ID = pr.ID AND rc.PURGE_DT IS NULL AND rc.RECORD_TYPE_CD = 'G'
			JOIN ESCROW_REQUIRED_COVERAGE_RELATE rel on rel.ESCROW_ID = esc.ID AND rel.REQUIRED_COVERAGE_ID = rc.ID AND rel.PURGE_DT IS NULL
			JOIN REQUIRED_ESCROW re ON re.REQUIRED_COVERAGE_ID = rc.ID AND re.ACTIVE_IN = 'Y' AND re.PURGE_DT IS NULL
			CROSS APPLY (SELECT COUNT(*) WI_COUNT
						FROM PROCESS_LOG_ITEM pli 
						JOIN WORK_ITEM wi ON wi.RELATE_ID = pli.PROCESS_LOG_ID AND wi.WORKFLOW_DEFINITION_ID = @escrowWorkflowDefId AND wi.STATUS_CD NOT IN ('Complete', 'Withdrawn')
												AND wi.PURGE_DT IS NULL
						WHERE pli.RELATE_ID = esc.ID 
						AND pli.RELATE_TYPE_CD = 'Allied.Unitrac.Escrow' 
						AND pli.PURGE_DT IS NULL AND pli.STATUS_CD = 'COMP') wi	
		WHERE pr.LENDER_ID = @lenderId 
		AND esc.STATUS_CD = 'OPEN'	
		AND wi.WI_COUNT = 0
	END

	INSERT INTO #FinalResults
	SELECT esc.ID
		, ln.NUMBER_TX
		, esc.SUB_STATUS_CD
		, rcEscSubStatus.MEANING_TX
		, esc.END_DT GOOD_THRU_DT
		, bic.NAME BOR_INS_COMP
		, remitAdd.LINE_1_TX REMIT_ADD_LINE1
		, remitAdd.LINE_2_TX REMIT_ADD_LINE2
		, remitAdd.CITY_TX REMIT_ADD_CITY
		, remitAdd.STATE_PROV_TX REMIT_ADD_STATE
		, esc.PAYEE_CODE_TX PAYEE_CD
		, rcRefCoverage.MEANING_TX COVERAGE
		, CONCAT(rc.INSURANCE_SUB_STATUS_CD, rc.INSURANCE_STATUS_CD) AS COVERAGE_STATUS
		, esc.PREMIUM_NO PAYMENT_AMOUNT
		, esc.AMOUNT_TYPE_CD PAYMENT_TYPE
		, esc.DUE_DT DUE_DATE
		, lastIhAction.UPDATE_DT AS INTERACTION_DATE
		, rcIh.MEANING_TX AS INTERACTION_TYPE
		, 'WI:' + CAST(tmp.ESCROW_WORKITEM_ID AS NVARCHAR) KEY_FIELD
		, CASE WHEN tmp.ESCROW_WORKITEM_ID IS NOT NULL THEN 'WORKITEM' ELSE NULL END
		, tmp.ESCROW_WORKITEM_ID
		, esc.CONTROL_NUMBER_TX CONTROL_NUMBER
		, wiEV.ID ESCROW_VERIFICATION_WORKITEM_ID
		, wiEsc.CONTENT_XML
		, wiEsc.STATUS_CD WORK_ITEM_STATUS_CD
		, col.LOAN_ID
		, esc.PREMIUM_NO
		, esc.OTHER_NO
		, ih.DOCUMENT_ID
	FROM ESCROW esc
		JOIN #escWIResults tmp ON tmp.ESCROW_ID = esc.ID
		JOIN PROPERTY pr ON pr.ID = esc.PROPERTY_ID AND pr.PURGE_DT IS NULL AND pr.RECORD_TYPE_CD = 'G'
		JOIN REQUIRED_COVERAGE rc ON rc.PROPERTY_ID = pr.ID AND rc.PURGE_DT IS NULL AND rc.RECORD_TYPE_CD = 'G'
		JOIN ESCROW_REQUIRED_COVERAGE_RELATE rel on rel.ESCROW_ID = esc.ID AND rel.REQUIRED_COVERAGE_ID = rc.ID AND rel.PURGE_DT IS NULL
		JOIN COLLATERAL col ON col.PROPERTY_ID = pr.ID AND col.PURGE_DT IS NULL AND col.PRIMARY_LOAN_IN = 'Y'
		JOIN LOAN ln ON ln.ID = col.LOAN_ID AND ln.RECORD_TYPE_CD = 'G' AND ln.PURGE_DT IS NULL
		JOIN REF_CODE rcRefCoverage ON rcRefCoverage.CODE_CD = rc.TYPE_CD AND rcRefCoverage.DOMAIN_CD = 'Coverage'
		JOIN REF_CODE rcEscSubStatus ON rcEscSubStatus.DOMAIN_CD = 'EscrowSubStatus' AND rcEscSubStatus.CODE_CD = esc.SUB_STATUS_CD AND rcEscSubStatus.PURGE_DT IS NULL
		JOIN REQUIRED_ESCROW re ON re.REQUIRED_COVERAGE_ID = rc.ID AND re.ACTIVE_IN = 'Y' AND re.PURGE_DT IS NULL
		LEFT JOIN WORK_ITEM wiEsc ON wiEsc.RELATE_ID = tmp.ESCROW_WORKITEM_ID AND wiEsc.WORKFLOW_DEFINITION_ID = @escrowWorkflowDefId AND wiEsc.STATUS_CD NOT IN ('Complete', 'Withdrawn')
		LEFT JOIN ESCROW_VERIFICATION ev ON ev.ESCROW_ID = esc.ID AND ev.PURGE_DT IS NULL
		LEFT JOIN WORK_ITEM wiEV ON wiEV.RELATE_ID = ev.ID AND wiEV.WORKFLOW_DEFINITION_ID = @escrowVerificationWorkflowDefId AND wiEV.STATUS_CD NOT IN ('Complete', 'Withdrawn')
		LEFT JOIN BORROWER_INSURANCE_COMPANY bic ON bic.ID = esc.BIC_ID AND bic.PURGE_DT IS NULL
		LEFT JOIN ADDRESS remitAdd ON remitAdd.ID = esc.REMITTANCE_ADDR_ID AND remitAdd.PURGE_DT IS NULL
		LEFT JOIN INTERACTION_HISTORY ih ON esc.ID = ih.RELATE_ID AND ih.PURGE_DT IS NULL AND ih.RELATE_CLASS_TX = 'Allied.UniTrac.Escrow'
		CROSS APPLY (SELECT TOP 1 TYPE_CD, ih.NOTE_TX, UPDATE_DT FROM INTERACTION_HISTORY ih WHERE ih.PROPERTY_ID = pr.ID 
				AND ih.PURGE_DT IS NULL 
				AND (ih.SPECIAL_HANDLING_XML.value('(/SH/RC/node())[1]','BIGINT') IS NULL OR ih.SPECIAL_HANDLING_XML.value('(/SH/RC/node())[1]','BIGINT') = rc.ID)
			ORDER BY ID DESC) lastIhAction
		JOIN REF_CODE rcIh on rcIh.CODE_CD = lastIhAction.TYPE_CD AND rcIh.DOMAIN_CD = 'InteractionHistoryType'
	WHERE pr.LENDER_ID = @lenderId


	SELECT DISTINCT
		fr.ID,
		fr.LOAN_NUMBER,
		fr.STATUS_CD,
		fr.STATUS_MEANING,
		fr.GOOD_THRU_DT,
		CASE
			WHEN ISNULL(fr.CONTROL_NUMBER, '') = '' THEN fr.BOR_INS_COMP 
			ELSE fr.BOR_INS_COMP + ' <br />CN:' + ISNULL(fr.CONTROL_NUMBER, '')
		END	AS BOR_INS_COMP,
		fr.REMIT_ADD_LINE1,
		fr.REMIT_ADD_LINE2,
		fr.REMIT_ADD_CITY,
		fr.REMIT_ADD_STATE,
		fr.PAYEE_CD,
		fr.COVERAGE,
		fr.COVERAGE_STATUS,
		fr.PAYMENT_AMOUNT,
		fr.PAYMENT_TYPE,
		fr.DUE_DATE,
		fr.INTERACTION_DATE,
		fr.INTERACTION_TYPE,
		fr.KEY_FIELD,
		fr.KEY_TYPE,
		fr.ESCROW_WORKITEM_ID,
		fr.CONTROL_NUMBER,
		fr.ESCROW_VERIFICATION_WORKITEM_ID,
		fr.CONTENT_XML.value('(/Content/Escrow/EscrowMode)[1]', 'NVARCHAR(20)') ESCROW_MODE,
		fr.WORK_ITEM_STATUS_CD,
		fr.LOAN_ID,
		fr.PREMIUM_NO,
		fr.OTHER_NO,
		fr.DOCUMENT_ID
	FROM #FinalResults fr
	ORDER BY GOOD_THRU_DT ASC

END

GO

